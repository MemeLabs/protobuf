plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.protobuf'
    id 'com.squareup.wire'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.14.0'
    }

    plugins {
        ktrpc {
            path = '/usr/local/bin/protoc-gen-ktrpc'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            ktrpc {}
        }
    }
}

wire {
    sourcePath {
        srcDir '../proto'
    }

    kotlin {
        includes = ['memelabs.protobuf.apis.*']
        out = 'src/main/java'
    }
}

task deleteWireRpc(type: Delete) {
    delete fileTree('src/main/java/memelabs/protobuf/apis') {
        include '**/Grpc*', '**/*Client.kt'
    }
}

generateProtos.finalizedBy deleteWireRpc

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            proto {
                srcDir '../proto'
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'com.squareup.wire:wire-runtime:3.6.0'
    implementation 'com.squareup.wire:wire-grpc-client:3.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9'

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    protobuf files('/usr/local/include/')
    protobuf files('/usr/include/')
}
